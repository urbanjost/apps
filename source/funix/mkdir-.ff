program demo_system_mkdir
use M_kracken, only : kracken,lget,sgets,IPvalue
use M_system,  only : system_mkdir, system_perror
use M_system,  only : R_GRP,R_OTH,R_USR,RWX_G,RWX_O
use M_system,  only : RWX_U,W_GRP,W_OTH,W_USR,X_GRP,X_OTH,X_USR
use M_system,  only : DEFFILEMODE, ACCESSPERMS
use M_io,      only : dirname
implicit none
character(len=IPvalue),allocatable :: filenames(:)
character(len=:),allocatable       :: fname
character(len=4096),allocatable    :: above(:)
character(len=4096)                :: scratch
integer                            :: ierr, i, j
logical                            :: verbose
logical                            :: parents
   call kracken('mkdir','-help .F. -version .F. -verbose .F. -parents .F. ')  ! define and crack the command line
   call help_usage(lget('mkdir_help'))                                        ! if -help display help and exit
   call help_version(lget('mkdir_version'))                                   ! if -version display help and exit
   filenames=sgets('mkdir_oo')                                                ! get directory names
   verbose=lget('mkdir_verbose')                                              ! get -verbose switch
   parents=lget('mkdir_parents')                                              ! get -parent switch

   do i=1,size(filenames)
      fname=trim(filenames(i))
      if(parents)then                                                         ! build an array of all the directory names
         above=[character(len=4096) :: fname ]
         scratch=fname
         do
            scratch=dirname(scratch)                         ! remove leaf from pathname and repeat till pathname leafs are consumed
            if(scratch.eq.'')exit
            if(scratch.eq.'.')exit
            above=[above,scratch]
         enddo
         do j=size(above),1,-1                                                ! start with shortest name and do a mkdir on them all
            ierr = system_mkdir(above(j), IANY([W_USR, R_USR, X_USR]))        ! make directory
            if(verbose)then
               if(ierr.ne.0)then
                  call system_perror('*mkdir-*')
               else
                  write(*,'(a,a)')'*mkdir-* : created directory ', trim(above(j))
               endif
            endif
         enddo
      else                                                                    ! no -parent option
         ierr = system_mkdir(fname, IANY([W_USR, R_USR, X_USR]))              ! make directory
         if(ierr.ne.0)then
            call system_perror('*mkdir-*:'//fname)
         elseif(verbose)then
            write(*,'(a,a)')'*mkdir-* : created directory ',fname
         endif
      endif
   enddo
contains
$!@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
$BLOCK HELP -file mkdir-.1.man
NAME
     mkdir-(1f) - [FUNIX:FILESYSTEM] call mkdir(3c) to make directories
     (LICENSE:PD)
SYNOPSIS
     mkdir- DIRECTORY ... [OPTION]...
DESCRIPTION
       Create the DIRECTORY(ies), if they do not already exist.
       The file permission mode by default is "a=rxw-umask".
OPTIONS
   DIRECTORY  directory pathnames. Limited to 4096 characters per pathname.
   --parents  no error if existing, make parent directories as needed
$!   --mode MODE  set file mode (as in chmod), not a=rwx - umask
   --verbose  print a message for each created directory
   --help     display this help and exit
   --version  output version information and exit
EXAMPLE
  Samples:

   # silently make specified directory and any needed parent directories
   mkdir- A/B/C -parents

   # show creation of three directories
   mkdir- A B C
AUTHOR
   John S. Urban
LICENSE
   Public Domain
$BLOCK END
$!@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
$BLOCK VERSION
PRODUCT:        GPF (General Purpose Fortran) utilities and examples
PROGRAM:        mkdir-(1f)
DESCRIPTION:    Create the specified directories if they do not already exist.
VERSION:        1.0, 2016-12-04
AUTHOR:         John S. Urban
REPORTING BUGS: http://www.urbanjost.altervista.org/
HOME PAGE:      http://www.urbanjost.altervista.org/index.html
$BLOCK END
$!@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
end program demo_system_mkdir
$!@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
